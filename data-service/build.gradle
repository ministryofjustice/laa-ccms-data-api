apply plugin: 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'

dependencies {

    implementation project(':data-api')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'

    //Metrics
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    //Enable access token authentication
    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'

    //Enable Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.12'

    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Make Spring Boot test and integration test dependencies available to the integrationTest source set
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Include data-jpa-test starter for @DataJpaTest annotation (Spring Boot 4+)
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'

    //Used for integration tests (Testcontainers 2.0+ required for Spring Boot 4)
    testImplementation 'org.testcontainers:testcontainers:2.0.2'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter:2.0.2'
    testImplementation 'org.testcontainers:testcontainers-oracle-free:2.0.2'
    testImplementation 'com.h2database:h2'

    // Explicitly add integration test dependencies to ensure availability on CI runners
    integrationTestImplementation 'org.testcontainers:testcontainers:2.0.2'
    integrationTestImplementation 'org.testcontainers:testcontainers-junit-jupiter:2.0.2'
    integrationTestImplementation 'org.testcontainers:testcontainers-oracle-free:2.0.2'
    integrationTestImplementation 'com.h2database:h2'
}

test {
    useJUnitPlatform()

    // Hide warning for dynamic loading of agents https://github.com/mockito/mockito/issues/3037
    jvmArgs '-XX:+EnableDynamicAgentLoading'

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/DataApplication.class'])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/DataApplication.class'])
        }))
    }
}
